plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.72'
    id 'org.jlleitschuh.gradle.ktlint' version '9.3.0'
    id 'org.jlleitschuh.gradle.ktlint-idea' version '9.3.0'
}

group 'org.example'
version '1.0'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

    // Junit 5 Runner + Kotest Style + MockK
    def kotest_version = '4.1.3'
    testImplementation "io.mockk:mockk:1.10.0"
    testImplementation "io.kotest:kotest-runner-console-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-runner-junit5-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-assertions-core-jvm:$kotest_version"
    testImplementation "io.kotest:kotest-property-jvm:$kotest_version"
}

compileKotlin {
    kotlinOptions.jvmTarget = "1.8"
}
compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

test {
    useJUnitPlatform()
}

jar {
    manifest {
        attributes(
                'Main-Class': 'org.example.sandbox.MainKt'
        )
    }

    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.findAll { it.name.endsWith('jar') }.collect { zipTree(it) }
    }
}

task runApp(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'org.example.sandbox.MainKt'
}